
#Область СлужебныйПрограммныйИнтерфейс

// Заполняет данные по документам в базе за период
//
// Параметры:
//  ТаблицаДляЗаполнения  - ТаблицаЗначений - таблица для заполнения; 
//  ДатаНачала - Дата - начальная дата получения данных; 
//  ДатаОкончания - Дата - конечная дата получения данных. 
//
Процедура ДанныеПоДокументамВБазеЗаПериод(ТаблицаДляЗаполнения, ДатаНачала, ДатаОкончания) Экспорт  
    
    ТекстЗапроса = "";   
    ЗаполнитьИнформациейПоМетаданным(ТаблицаДляЗаполнения, ТекстЗапроса);
    ЗаполнитьДаннымиИзБазы(ТаблицаДляЗаполнения, ТекстЗапроса, ДатаНачала, ДатаОкончания);
        
КонецПроцедуры

#КонецОбласти        

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьИнформациейПоМетаданным(ТаблицаДляЗаполнения, ТекстЗапроса)
    
    ПроведениеРазрешить = Метаданные.СвойстваОбъектов.Проведение.Разрешить;
    ОперативноеПроведениеРазрешить = Метаданные.СвойстваОбъектов.ОперативноеПроведение.Разрешить;
    
    Для Каждого СтрДокумент Из Метаданные.Документы Цикл 
        СтрокаДляЗаполнения = ТаблицаДляЗаполнения.Добавить();
        СтрокаДляЗаполнения.ИдентификаторВидаДокумента = СтрДокумент.Имя; 
        СтрокаДляЗаполнения.ПредставлениеВидаДокумента = СтрДокумент.Синоним; 
        СтрокаДляЗаполнения.РежимУправленияБлокировками = СтрДокумент.РежимУправленияБлокировкойДанных; 
        СтрокаДляЗаполнения.ПроведениеРазрешено = СтрДокумент.Проведение = ПроведениеРазрешить; 
        СтрокаДляЗаполнения.ОперативноеПроведениеРазрешено = 
            СтрДокумент.ОперативноеПроведение = ОперативноеПроведениеРазрешить; 
            
        СписокЗначений = СписокДвигаемыхРегистровНакопления(СтрДокумент.Движения);   
        СтрокаДляЗаполнения.СписокДвигаемыхРегистровНакопления.ЗагрузитьЗначения(СписокЗначений); 
            
        ДобавитьТекстЗапросаПоДокументу(ТекстЗапроса, СтрДокумент.Имя);                
    КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьДаннымиИзБазы(ТаблицаДляЗаполнения, ТекстЗапроса, ДатаНачала, ДатаОкончания)
    
    Запрос = Новый Запрос;
    Запрос.Текст = ТекстЗапроса;
    Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
    Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
    РезультатЗапроса = Запрос.Выполнить();
    
    Выборка = РезультатЗапроса.Выбрать();
    
    Пока Выборка.Следующий() Цикл
        Отбор = Новый Структура("ИдентификаторВидаДокумента", Выборка.ИмяДокумента);
        НайденныеСтроки = ТаблицаДляЗаполнения.НайтиСтроки(Отбор);
        Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
            ЗаполнитьЗначенияСвойств(НайденнаяСтрока, Выборка);    
        КонецЦикла;
    КонецЦикла;
    
    ТаблицаДляЗаполнения.Сортировать("КоличествоДокументовВПериоде Убыв");
    
КонецПроцедуры

Функция СписокДвигаемыхРегистровНакопления(Движения)
    
    СписокДвигаемыхРегистров = Новый Массив;
    Для Каждого Движение Из Движения Цикл
        Если Метаданные.РегистрыНакопления.Найти(Движение.Имя) <> Неопределено Тогда 
            СписокДвигаемыхРегистров.Добавить(Движение.Имя);    
        КонецЕсли;        
    КонецЦикла;
    
    Возврат СписокДвигаемыхРегистров;
    
КонецФункции
 
Процедура ДобавитьТекстЗапросаПоДокументу(ТекстЗапроса, ИмяДокумента)
    
    Если ЗначениеЗаполнено(ТекстЗапроса) Тогда 
        ТекстЗапроса = СтрШаблон("%1%2", ТекстЗапроса, ТекстЗапросаПоДокументу(ИмяДокумента));
    Иначе
        ТекстЗапроса = ТекстЗапросаПоПервомуДокументу(ИмяДокумента);     
    КонецЕсли;
    
КонецПроцедуры

Функция ТекстЗапросаПоПервомуДокументу(ИмяДокумента)
    
    ТекстЗапросаПоДокументу = 
    "ВЫБРАТЬ
    |   ""&ИмяДокумента"" КАК ИмяДокумента,
    |   СУММА(1) КАК КоличествоДокументовВПериоде,
    |   СУММА(ВЫБОР
    |           КОГДА Документ.Проведен
    |               ТОГДА 1
    |           ИНАЧЕ 0
    |       КОНЕЦ) КАК КоличествоПроведенныхДокументовВПериоде,
    |   СУММА(ВЫБОР
    |           КОГДА Документ.ПометкаУдаления
    |               ТОГДА 1
    |           ИНАЧЕ 0
    |       КОНЕЦ) КАК КоличествоПомеченныхНаУдалениеДокументовВПериоде
    |ИЗ
    |   Документ.&ИмяДокумента КАК Документ
    |ГДЕ
    |   Документ.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания";
    
    ТекстЗапросаПоДокументу = СтрЗаменить(ТекстЗапросаПоДокументу, "&ИмяДокумента", ИмяДокумента);
    
    Возврат ТекстЗапросаПоДокументу;
    
КонецФункции

Функция ТекстЗапросаПоДокументу(ИмяДокумента)
    
    ТекстЗапросаПоДокументу = 
    "
    |ОБЪЕДИНИТЬ ВСЕ
    |
    |ВЫБРАТЬ
    |   ""&ИмяДокумента"",
    |   СУММА(1),
    |   СУММА(ВЫБОР
    |           КОГДА Документ.Проведен
    |               ТОГДА 1
    |           ИНАЧЕ 0
    |       КОНЕЦ),
    |   СУММА(ВЫБОР
    |           КОГДА Документ.ПометкаУдаления
    |               ТОГДА 1
    |           ИНАЧЕ 0
    |       КОНЕЦ)
    |ИЗ
    |   Документ.&ИмяДокумента КАК Документ
    |ГДЕ
    |   Документ.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания";
    
    ТекстЗапросаПоДокументу = СтрЗаменить(ТекстЗапросаПоДокументу, "&ИмяДокумента", ИмяДокумента);
    
    Возврат ТекстЗапросаПоДокументу;
    
КонецФункции

#КонецОбласти